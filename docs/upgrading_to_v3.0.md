# Upgrade Guidance
Before moving forward with adopting components of v3, please review the list of breaking changes below. You can find a complete list of features, bug fixes and other updates in the [Changelog](https://github.com/terraform-google-modules/terraform-example-foundation/blob/master/CHANGELOG.md).

**Important:** There is no in-place upgrade path from v2 to v3

## Breaking Changes

- Upgrade minimum required Terraform version to 1.3.0, previously was 0.13.7.
- Added Remote State. This feature centralizes frequently used terraform variables in a remote state bucket (Google Storage) so users will not have the need to re declare them while deploying the different steps of foundation. Now, each step does requires access (read/write) to this single remote state bucket in order to be properly deployed.
- Added Granular Service Account (SA). In previous versions, only one SA was used to deployed all steps. This end up having an excess of permissions give to this SA. Now, each has its own SA with very limited permissions.
- Bring your Service Account (BYOSA). It lets terraform commands be impersonated with a specified SA.
- 3-networks split into two. Networks step supported both networks topologies, Dual-SVPC and Hub-and-Spoke. Currently, these have been  separated into two different implementations.

**Note:** You can use newer Terraform versions directly on top of already deployed  configurations with older version. However, you should take into account that doing so will prevent you from using older versions. Also, this action will update terraform state and dependencies (external modules and provider's resources). It is advisable to backup your terraform state before trying an upgrade.

## Adapting New Features

In case you want to adapt newer features, you can update your terraform configuration following foundation's v3 practices and codebase and then applying given configuration with `terraform apply`. You should verify that you are using the required terraform version and gcloud version.

Although new configurations should consider additions only, it might end up with some resources being moved from one module to another. In this scenario, we recommend users to review terraform official documentation for [moved blocks](https://developer.hashicorp.com/terraform/tutorials/configuration-language/move-config).

In the following subsections we give some examples on how these moved blocks can be implemented.

**Note:** As stated previously, there is not direct path for upgrading the foundation's codebase. So, we discourage users to try to leverage their terraform configuration directly applying foundation v3. Instead, we suggest users to deeply read and study `terraform plan` output before updating their configuration.

### Module-to-Module

Consider a 0-bootstrap foundation v2 deployment without no modifications. If we try to migrate to v3 we will observe the following as the summarize of the output of `terraform plan`.

    Plan: 165 to add, 2 to change, 67 to destroy.

You can review `module.cloudbuild_bootstrap.module.cloudbuild_project` module and observe that there are plenty of resources that are destroyed and created again in a similar module.

    # module.cloudbuild_bootstrap.module.cloudbuild_project.module.project-factory.module.project_services.google_project_service.project_services["admin.googleapis.com"] will be destroyed
    # (because google_project_service.project_services is not in configuration)
    - resource "google_project_service" "project_services" {
      - disable_dependent_services = true -> null
      - disable_on_destroy         = false -> null
      - id                         = "prj-b-cicd-9aee/admin.googleapis.com" -> null
      - project                    = "prj-b-cicd-9aee" -> null
      - service                    = "admin.googleapis.com" -> null
    }

And

    # module.tf_source.module.cloudbuild_project.module.project-factory.module.project_services.google_project_service.project_services["admin.googleapis.com"] will be created
    + resource "google_project_service" "project_services" {
      + disable_dependent_services = true
      + disable_on_destroy         = false
      + id                         = (known after apply)
      + project                    = (known after apply)
      + service                    = "admin.googleapis.com"
    }

We can omit this behavior if we provide following block.

    # cloudbuild bootstrap
    moved {
        from = module.cloudbuild_bootstrap.module.cloudbuild_project
        to   = module.tf_source.module.cloudbuild_project
    }

Now, on the majority of modules this should be enough. However, if part of a module's ID is composed of a autogenerated random string, you will need to hardcode this module's ID in the moved resource.

In this specific example you will need to you will need to hard-code CICD project's ID at `module.tf_source`.

Now, we can verify that the number of resources to be destroyed have been reduced considerably.

    Plan: 146 to add, 4 to change, 48 to destroy.
### Backups

You can save resources from being destroyed and instead make a copy of them as a backup. The following blocks of code shows how to save KMS keys no longer being part of the foundation v3 configuration.

```hcl
terraform-example foundation/0-bootstrap/backup.tf.example

resource "google_kms_crypto_key" "backup_tf_key" {
    destroy_scheduled_duration = "86400s"
    import_only                   = false
    key_ring                      = "projects/prj-b-cicd-9aee/locations/us-central1/keyRings/tf-keyring"
    labels                        = {}
    name                          = "tf-key"
    purpose                       = "ENCRYPT_DECRYPT"
    skip_initial_version_creation = false

    version_template {
        algorithm        = "GOOGLE_SYMMETRIC_ENCRYPTION"
        protection_level = "SOFTWARE"
    }
}

resource "google_kms_key_ring" "backup_tf_keyring" {
    location = "us-central1"
    name     = "tf-keyring"
    project  = "prj-b-cicd-9aee"
}
```

```hcl
terraform-example foundation/0-bootstrap/moved.tf.exmaple

moved {
    from = module.cloudbuild_bootstrap.google_kms_crypto_key.tf_key
    to   = google_kms_crypto_key.backup_tf_key
}

moved {
    from = module.cloudbuild_bootstrap.google_kms_key_ring.tf_keyring
    to   = google_kms_key_ring.backup_tf_keyring
}
```

